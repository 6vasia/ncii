#!/usr/bin/env perl

use strict;
use warnings;
use utf8;

use Curses;
use Curses::UI;
use YAML;
use II::Point;
use II::Cache::YAML;
use II::Misc qw(logger logger_facility);
use Env qw/HOME/;
#use Data::Dumper;

binmode STDIN, ':utf8';
binmode STDOUT, ':utf8';

my $cui;
my $cfg = {};
my $ii;
my $echoes = {};
my $messages = {};
my $cache;

sub config
{
    my $cfgpath = $HOME ? $HOME . '/.config/ncii' : '.';
    -d $cfgpath or mkdir $cfgpath or die "Couldn't mkdir $cfgpath: $!";
    $cfgpath = '.' if -e './ncii.yaml';
    $cfg = YAML::LoadFile ($cfgpath.'/ncii.yaml') if -e $cfgpath.'/ncii.yaml';
    if (defined $cfg->{node} and $cfg->{node} =~ m@\Wu/$@) {
        $cfg->{node} =~ s@u/$@@;
    }
}

sub load_cache
{
    my $cachepath = $HOME ? $HOME . '/.config/ncii' : '.';
    $cui->status ("Loading cache") if defined $cui;
    $cache = II::Cache::YAML->new ($cfg->{node}, $cachepath);
    $echoes = $cache->echoes;
    $messages = $cache->messages;
    $cui->nostatus if defined $cui;
}

sub dump_cache
{
    logger ("info", "Dumping cache");
    $cui->status ("Dumping cache") if defined $cui;
    if (defined $cache){
        $cache->echoes($echoes);
        $cache->messages($messages);
        $cache->dump;
    }
    $cui->nostatus if defined $cui;
}

sub msg_title ($$)
{
    my ($m, $width) = @_;
    my @lt = localtime($m->{date});
    my $ts = sprintf(" %02d:%02d %02d.%02d.%04d",
            $lt[2], $lt[1], $lt[3], $lt[4], $lt[5]+1900
        );
    my $title = sprintf ("%4d: %s (%s -> %s)",
        $m->{no}, $m->{subj}, $m->{from}, $m->{to});
    if (length($title) > $width - length($ts)) {
        return substr ($title, 0, $width - length($ts)).$ts;
    } else {
        return $title." "x($width - length($ts) - length($title)).$ts;
    }
}

sub fetch
{
    local $_;
    my @errors = ();
    
    unless (defined $ii){
        my $authstr = 'guest';
        $authstr = $cfg->{authstr} if (defined $cfg->{authstr} and $cfg->{authstr});
        $ii = II::Point->new ($cfg->{node}, $authstr);
    }
    for my $echo (@{$cfg->{echoes}}) {
        {
            logger ("info", "Fetching %s", $echo);
            $cui->status ("Loading $echo...");
            
            my %msgs = $ii->fetch_echoes($echo);
            @errors = $ii->pop_errors;
            if (scalar @errors){
                logger ("warn", "Error fetching %s: %s", $echo, $_) for (@errors);
                $cui->error("There were errors fetching $echo. See log for details.");
            }
            $echoes->{$echo} = $msgs{$echo};
            logger ("info", "Fetched %s: %d", $echo, scalar @{$msgs{$echo}});
            
            $cui->nostatus;
        }
        
        my @fetchlist = ();
        for my $m (@{$echoes->{$echo}}) {
            push @fetchlist, $m unless defined $messages->{$m};
        }
        if (scalar @fetchlist) {
            logger ("info", "fetching %d messages", scalar @fetchlist);
            $cui->status ("Loading messages for $echo");

            my %fetched = $ii->fetch_msgs(@fetchlist);
            @errors = $ii->pop_errors;
            if (scalar @errors) {
                logger ("warn", "Error fetching: %s", $_) for (@errors);
                $cui->error ("Error fetching messages. See log for detail.");
            }
            for my $msg (keys %fetched){
                $messages->{$msg} = $fetched{$msg};
                $messages->{$msg}{unread} = 1;
            }
            $cui->nostatus;
        }
        my $no = 1;
        for my $m (@{$echoes->{$echo}}){
            $messages->{$m}{no} = $no++;
        }
    }
}

sub load_echo ($)
{
    local $_;
    my $list = shift;
    my $echo = $list->title;
    my $strwidth = $list->canvaswidth;
    
    return unless $echo and $echoes->{$echo};
    logger ("info", "Loading %s", $echo);
    $list->values ([ reverse @{$echoes->{$echo}} ]);
    $list->labels ({ map { $_ => msg_title ($messages->{$_}, $strwidth) } @{$echoes->{$echo}} });
    $list->draw;
}

sub fullscreen
{
    my $viewwin = shift;
    $viewwin->focus;
    $viewwin->getobj('content')->focus;
}

sub ui
{
    my $echowidth = defined $cfg->{ui}{echolist_width} ? $cfg->{ui}{echolist_width} : 16;
    my $mainwin = $cui->add (
        'mainwin', 'Window',
        -border => 0,
    );
    my $echowin = $mainwin->add (
        'echowin', 'Window',
        -border => 1,
        -width => $echowidth
    );
    my $echolist = $echowin->add (
        'echolist', 'Listbox',
        -border => 0,
        -values => $cfg->{echoes},
    );
    my $msgwin = $mainwin->add (
        'msgwin', 'Window', 
        -border => 0,
        -x => $echowidth,
    );
    my $msglist = $msgwin->add (
        'msglist', 'Listbox',
        -border => 1,
        -height => $msgwin->canvasheight()/2,
        -vscrollbar => 'right',
    );
    my $preview = $msgwin->add (
        'prewiew', 'TextViewer', 
        -border => 1,
        -wrapping => 1,
        -y => int($msgwin->canvasheight()/2),
    );
    $mainwin->set_binding( sub {dump_cache; $cui->mainloopExit;} , "\cQ");
    $mainwin->set_binding( sub {
            $echowin->{-width} += 1;
            $echowin->layout;
            $msgwin->{-x} += 1;
            $msgwin->layout;
            $mainwin->draw;
        } , "\cL");
    $mainwin->set_binding( sub {
            $echowin->{-width} -= 1;
            $echowin->layout;
            $msgwin->{-x} -= 1;
            $msgwin->layout;
            $mainwin->draw;
        } , "\cK");
    $mainwin->set_binding(sub {
            $echolist->option_next;
            $echolist->option_select;
            $echolist->draw},
        "\cN");
    $mainwin->set_binding(sub {
            $echolist->option_prev;
            $echolist->option_select;
            $echolist->draw},
        "\cP");
    
    $echolist->onChange(sub {
            $msglist->title($echolist->get_active_value());
            load_echo($msglist);
            $msglist->draw;
        });
    $msglist->onSelectionChange(sub {
            $msglist->option_select;
            if ($msglist->get_active_value() and defined $messages->{$msglist->get_active_value()}){
                $preview->text($messages->{$msglist->get_active_value()}{content});
            }
        });
        
    $mainwin->set_binding(sub {
            fetch();
            load_echo ($msglist);
        }, "\cF");

    my $viewwin = $cui->add (
        'viewwin', 'Window',
        -border => 0,
    );
    my $vwhead = $viewwin->add (
        'head', 'Window',
        -border => 1,
        -title => 'Area %s | Message %d of %d',
        -y => 0,
        -height => 7
    );
    $vwhead->add (
        'date', 'Label'
    );
    my $vwbody = $viewwin->add (
        'content', 'TextViewer',
        -y => 8,
    );
    $vwbody->set_routine ('loose-focus', 'RETURN');
    
    $viewwin->set_binding (sub {$mainwin->focus}, "\cQ");
    $msglist->set_binding (sub {fullscreen($viewwin)}, KEY_ENTER());

    $echolist->set_selection(0);
    $msgwin->focus;
}

sub options_ui
{
    die "Config me first!";
}

logger_facility "ncii";
logger ("info", "=== Starting at %s ===", scalar localtime);

config;
$cui = new Curses::UI ( 
    -clear_on_exit => 1, 
    -mouse_support => 1,
    -color_support => 1,
    -debug => 0,
    -utf8 => 1,
);
load_cache;
ui;

if (not defined $cfg->{node}) {
    options_ui;
}

$cui->mainloop;
